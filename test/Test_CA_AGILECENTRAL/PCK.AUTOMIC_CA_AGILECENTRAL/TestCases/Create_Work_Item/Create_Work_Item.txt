*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        create_work_item
Test Template     Create Work Item Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    WorkspaceName        ProjectName        WorkItemName        Description       ScheduleState        CustomFields
Crete work item with providing valid inputs
                      [Tags]    main_scenarios
                      OK        ${Workspace_Name}    ${Project_Name}    ${Work_ItemName}    ${Description}    ${Schedule_State}    ${Custom_Fields}

Assertion with invalid workspace name
                      NOK       Invalid              ${Project_Name}    ${Work_ItemName}    ${Description}    ${Schedule_State}    ${Custom_Fields}

Assertion with invalid project name
                      NOK       ${Workspace_Name}    Invalid            ${Work_ItemName}    ${Description}    ${Schedule_State}    ${Custom_Fields}

Assertion with invalid Work item name
                      NOK       ${Workspace_Name}    ${Project_Name}    Invalid             ${Description}    ${Schedule_State}    ${Custom_Fields}

Assertion with invalid Description
                      NOK       ${Workspace_Name}    ${Project_Name}    ${Work_ItemName}    Invalid           ${Schedule_State}    ${Custom_Fields}

Assertion with invalid Schedule state
                      NOK       ${Workspace_Name}    ${Project_Name}    ${Work_ItemName}    ${Description}    Invalid              ${Custom_Fields}

Assertion with invalid Custom fields
                      NOK       ${Workspace_Name}    ${Project_Name}    ${Work_ItemName}    ${Description}    ${Schedule_State}    Invalid

Assert Server workspace name as mandatory field
                      NOK       ${Empty}             ${Project_Name}    ${Work_ItemName}    ${Description}    ${Schedule_State}    ${Custom_Fields}

Assert Project name as mandatory field
                      NOK       ${Workspace_Name}    ${Empty}           ${Work_ItemName}    ${Description}    ${Schedule_State}    ${Custom_Fields}

Assert Work item name as mandatory field
                      NOK       ${Workspace_Name}    ${Project_Name}    ${Empty}            ${Description}    ${Schedule_State}    ${Custom_Fields}

Assert Schedule state as mandatory field
                      NOK       ${Workspace_Name}    ${Project_Name}    ${Work_ItemName}    ${Description}    ${Empty}             ${Custom_Fields}

*** Keywords ***
Startup
    Log    *******Start Test "Create Work Item"*******
    Log    *******Connect to AE*******
    Connect AE

Create Work Item Template
    [Arguments]    ${OK_NOK}    ${WorkspaceName}    ${ProjectName}    ${WorkItemName}    ${Description}    ${ScheduleState}
    ...    ${CustomFields}
    [Documentation]    This test executes the common failure scenarios for action pack with create work item action.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK, NOK.
    ...    -${WorkspaceName}: This field specifies workspace name in which your project is located.
    ...    -${ProjectName}: This field specifies a new project name to the work item.
    ...    -${WorkItemName}: This field specifies a new work item name.
    ...    -${Description}: This field specifies new description to your work item.
    ...    -${ScheduleState}: This field specifies new schedule state to the work item.
    ...    -${CustomFields}: This field specifies list of other fields with values you want to set in format; ("Name1", "Value1"),("Name2","Value2")
    Generate String
    Init Action    ${_CREATE_WORK_ITEM_ACTION}
    Connect To Agile_Central
    Run Keyword If    '${WorkspaceName}' == 'Invalid'    Action Set    &UC4RB_AC_WORKSPACE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_WORKSPACE#    ${WorkspaceName}
    Run Keyword If    '${ProjectName}' == 'Invalid'    Action Set    &UC4RB_AC_PRJCT_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_PRJCT_NAME#    ${ProjectName}
    Run Keyword If    '${WorkItemName}' == 'Invalid'    Action Set    &UC4RB_AC_ITEM_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_ITEM_NAME#    ${WorkItemName}
    Run Keyword If    '${Description}' == 'Invalid'    Action Set    &UC4RB_AC_DESCRIPTION#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_DESCRIPTION#    ${Description}
    Run Keyword If    '${ScheduleState}' == 'Invalid'    Action Set    &UC4RB_AC_SCHEDULE_STATE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_SCHEDULE_STATE#    ${ScheduleState}
    Run Keyword If    '${CustomFields}' == 'Invalid'    Action Set    &UC4RB_AC_CUSTOM_FIELDS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_CUSTOM_FIELDS#    ${CustomFields}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_AC_WORK_ITEM_ID#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_AC_WORK_ITEM_ID#

Teardown
    Log    *******End Test "Create Work Item"*******

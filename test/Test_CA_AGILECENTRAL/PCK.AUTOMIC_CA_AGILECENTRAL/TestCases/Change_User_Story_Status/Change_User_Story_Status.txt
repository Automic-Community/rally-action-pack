*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        change_user_story_status
Test Template     Change User Story Status Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    WorkspaceName        UserStoryID        NewStatus
Change User Story Status with valid inputs
                      [Tags]    main_scenarios
                      OK        ${Workspace_Name}    ${UserStory_ID}    Blocked=true,Ready=false

Assert Workspace Name as Optinal Field
                      OK        ${EMPTY}             ${UserStory_ID}    Blocked=false,Ready=true

Assert action behavior with invalid Workspace
                      NOK       Invalid              ${UserStory_ID}    Blocked=true,Ready=false

Assert action behavior with invalid User Story ID
                      NOK       ${Workspace_Name}    Invalid            Blocked=true,Ready=false

Assert action beahvior with invalid Status
                      NOK       ${Workspace_Name}    ${UserStory_ID}    Invalid

Assert User Story ID as Mandatory Field
                      NOK       ${Workspace_Name}    ${EMPTY}           Blocked=true,Ready=false

Assert New Status as Mandatory Field
                      NOK       ${Workspace_Name}    ${UserStory_ID}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "CHANGE USER STORY STATUS"*******
    Log    *******Connect to AE*******
    Connect AE

Change User Story Status Template
    [Arguments]    ${OK_NOK}    ${WorkspaceName}    ${UserStoryID}    ${NewStatus}
    [Documentation]    This test is used for changing user story status.
    ...    -Precondition: To execute valid test, Existing user story id should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${WorkspaceName}: This field specifies the workspace name in which your project is located.
    ...    -${UserStoryID}: This field specifies the user story for which you want to change status.
    ...    -${NewStatus}: This field specifies the new status you want to set to story
    Generate String
    Init Action    ${_CHANGE_USER_STORY_STATUS}
    Connect To AgileCentral
    Run Keyword If    '${WorkspaceName}' == 'Invalid'    Action Set    &UC4RB_AC_WORKSPACE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_WORKSPACE#    ${WorkspaceName}
    Run Keyword If    '${UserStoryID}' == 'Invalid'    Action Set    &UC4RB_AC_ITEM_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_ITEM_ID#    ${UserStoryID}
    Run Keyword If    '${NewStatus}' == 'Invalid'    Action Set    &UC4RB_AC_ITEM_STATUS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_ITEM_STATUS#    ${NewStatus}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "CHANGE USER STORY STATUS"*******

*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        add_comment
Test Template     Add Comment Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    WorkspaceName        WorkItemType    WorkItemID         Comment
Add Comment with valid inputs
                      [Tags]    main_scenarios
                      OK        ${Workspace_Name}    User Story      ${UserStory_ID}    Generated

Assert Workspace Name as Optinal Field
                      OK        ${EMPTY}             User Story      ${UserStory_ID}    Generated

Assert action behavior with invalid Workspace
                      NOK       Invalid              User Story      ${UserStory_ID}    Generated

Assert action behavior with invalid User Story ID
                      NOK       ${Workspace_Name}    User Story      Invalid            Generated

Assert Work Item ID as Mandatory Field
                      NOK       ${Workspace_Name}    User Story      ${EMPTY}           Generated

Assert Comment as Mandatory Field
                      NOK       ${Workspace_Name}    User Story      ${UserStory_ID}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "ADD COMMENT"*******
    Log    *******Connect to AE*******
    Connect AE

Add Comment Template
    [Arguments]    ${OK_NOK}    ${WorkspaceName}    ${WorkItemType}    ${WorkItemID}    ${Comment}
    [Documentation]    This test is used for adding comment to user story.
    ...    -Precondition: To execute valid test, Existing user story id should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${WorkspaceName}: This field specifies the workspace name in which your work item is located.
    ...    -${WorkItemType}: This field specifies the type of work item to which you want to add comment
    ...    -${WorkItemID}: This field specifies the id of work item to which you want to add comment.
    ...    -${Comment}: This field specifies the comment you want to add.
    Generate String
    Init Action    ${_ADD_COMMENT}
    Connect To AgileCentral
    Run Keyword If    '${WorkspaceName}' == 'Invalid'    Action Set    &UC4RB_AC_WORKSPACE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_WORKSPACE#    ${WorkspaceName}
    Action Set    &UC4RB_AC_ITEM_TYPE#    ${WorkItemType}
    Run Keyword If    '${WorkItemID}' == 'Invalid'    Action Set    &UC4RB_AC_ITEM_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_ITEM_ID#    ${WorkItemID}
    Run Keyword If    '${Comment}' == 'Generated'    Action Set    &UC4RB_AC_ITEM_COMMENT#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_ITEM_COMMENT#    ${Comment}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "ADD COMMENT"*******

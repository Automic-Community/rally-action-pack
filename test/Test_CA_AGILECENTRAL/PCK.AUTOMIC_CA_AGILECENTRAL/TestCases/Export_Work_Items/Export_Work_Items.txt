*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        export_work_items
Test Template     Export Work Items Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    WorkspaceName        WorkItemType    Filters                                 FieldsToExport      PathForExport                     MaximumItems
Export 100 workitems while providing workspace name and work item type only
                      [Tags]    main_scenarios
                      OK        ${Workspace_Name}    User Story      ${EMPTY}                                ${EMPTY}            ${EMPTY}                          1

Assert Workspace Name as Optinal Field and exporting specific fields to execution directory
                      OK        ${EMPTY}             User Story      (Blocked = False)                       FormattedID,Name    ${EXECDIR}\\AutomationTest.csv    500

Assert action behavior with invalid Fields to Export
                      OK        ${Workspace_Name}    User Story      ${EMPTY}                                Invalid             ${EMPTY}                          10

Assert action behavior when Maximum Items is set to 0
                      OK        ${Workspace_Name}    User Story      ((Blocked = False)AND(Ready = True))    ${EMPTY}            ${EMPTY}                          0

Assert action behavior with invalid Workspace
                      NOK       Invalid              User Story      ${EMPTY}                                ${EMPTY}            ${EMPTY}                          50

Assert behavior with invalid Filters and asserting Path For Export as optinal field
                      NOK       ${EMPTY}             User Story      (abc=test)                              ${EMPTY}            ${EMPTY}                          100

Assert action behavior with invalid Path for Export
                      NOK       ${Workspace_Name}    User Story      ${EMPTY}                                ${EMPTY}            Invalid                           50

*** Keywords ***
Startup
    Log    *******Start Test "EXPORT WORK ITEMS"*******
    Log    *******Connect to AE*******
    Connect AE

Export Work Items Template
    [Arguments]    ${OK_NOK}    ${WorkspaceName}    ${WorkItemType}    ${Filters}    ${FieldsToExport}    ${PathForExport}
    ...    ${MaximumItems}
    [Documentation]    This test is used for exporting work items from agilecentral to an csv file.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${WorkspaceName}: This field specifies the workspace name in which your work item is located.
    ...    -${WorkItemType}: This field specifies the type of work item which you want to export
    ...    -${Filters}: This field specifies filters you want consider in export.
    ...    -${FieldsToExport}: This field specifies the field names you want to export as comma separated.
    ...    -${PathForExport}: This field specifies the full path in which user stories will be exported.
    ...    -${MaximumItems}: This field specifies the maximum number of work items to be exported. Zero (0) means no limitation.
    Generate String
    Init Action    ${_EXPORT_WORK_ITEMS}
    Connect To AgileCentral
    Run Keyword If    '${WorkspaceName}' == 'Invalid'    Action Set    &UC4RB_AC_WORKSPACE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_WORKSPACE#    ${WorkspaceName}
    Action Set    &UC4RB_AC_ITEM_TYPE#    ${WorkItemType}
    Action Set    &UC4RB_AC_ITEM_FILTER#    ${Filters}
    Run Keyword If    '${FieldsToExport}' == 'Generated'    Action Set    &UC4RB_AC_ITEM_FIELDS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_ITEM_FIELDS#    ${FieldsToExport}
    Run Keyword If    '${PathForExport}' == 'Invalid'    Action Set    &UC4RB_AC_EXPORT_PATH#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_EXPORT_PATH#    ${PathForExport}
    Action Set    &UC4RB_AC_ITEMS_LIMIT#    ${MaximumItems}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Count Value Should Be    &UC4RB_AC_TOTAL_RESULT_COUNT#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_AC_TOTAL_RESULT_COUNT#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable File Path Should Be    &UC4RB_AC_EXPORT_FILE_PATH#    .csv    ELSE    Action Object Variable Should Not Be Found
    ...    &UC4RB_AC_EXPORT_FILE_PATH#

Teardown
    Log    *******End Test "EXPORT WORK ITEMS"*******
